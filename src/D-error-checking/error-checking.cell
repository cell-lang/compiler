[CoreWFError] core_wf_errors(Program prg) {
  let program = prg:
    errs = core_wf_errors(program = prg, typedefs = schema_and_typedefs);
  ;
  return errs;
}

////////////////////////////////////////////////////////////////////////////////

implicit program : Program, typedefs : [TypeName -> NeType] {
  [CoreWFError] core_wf_errors {
    fndefs_families = group_by(program.fndefs, family_id($.id));
    functions_errs = union([function_family_errors(fs) : unused_var, fs <- fndefs_families]);
    return typedefs_errors & functions_errs & databases_errors & automata_errors;
  }

  //////////////////////////////////////////////////////////////////////////////

  [CoreWFError] typedefs_errors {
    shallow_refs_map = [n -> direct_refs(t) : n, t <- typedefs];
    deep_refs_map = transitive_closure(shallow_refs_map);
    return [non_nested_self_ref(t) : t, ts <- deep_refs_map, ts(t)];

    [TypeName] direct_refs(NeType type) =
      type_ref(type_symbol() ts?)     = [type_name(ts, 0)],
      type_ref(par_type_symbol() ts?) = [type_name(ts.symbol, |ts.params|)],
      union_type(ts?)                 = union([direct_refs(t) : t <- ts]),
      _                               = [];
  }

  //////////////////////////////////////////////////////////////////////////////

  [CoreWFError] function_family_errors([+Function] functions) {
    ord_fns = rand_sort(functions);
    return [overlapping_signatures(f1, f2) : f1 @ i1 <~ ord_fns, f2 <~ drop(ord_fns, i1+1), overlap(f1, f2)];

    Bool overlap(Function f1, Function f2) =
      not (a1, a2 <~ zip(f1.args, f2.args) : are_ptype_disjoint(right(a1), right(a2)));
  }

  //////////////////////////////////////////////////////////////////////////////

  [CoreWFError] databases_errors {
    db_errs = union([database_errors(db) : db <- program.data_bases]);

    shallow_deps = [db.name -> values(db.nested_dbs) & set(db.links) : db <- program.data_bases];
    deep_deps = transitive_closure(shallow_deps);
    rec_errs = [recursively_defined_auto_or_db(automaton_by_id(db_type)) : db_type, deps <- deep_deps, deps(db_type)];

    return db_errs & rec_errs;
  }


  [CoreWFError] database_errors(Database database) {
    rel_var_errs = union(
      [rel_var_errors(v, info.signatures, info.keys, info.symmetric) : v, info <- database.rel_vars]
    );

    foreign_key_errs = union([foreign_key_errors(database, f) : f <- database.foreign_keys]);

    method_errs = union([
      [overlapping_signatures(m1, m2) : m1, m2 <- symmetric_cart_prod(ms), overlap(m1, m2)]
      : unused_var, ms <- group_by(database.methods, ($.name, arity($)))
    ]);

    updates_errs = union([
      [overlapping_signatures(u1, u2) : u1, u2 <- symmetric_cart_prod(us), overlap(u1, u2)]
      : unused_var, us <- group_by(database.updates, ($.name, arity($)))
    ]);

    handler_errs = [
      overlapping_signatures(h1, h2)
      : h1, h2 <- symmetric_cart_prod(database.handlers), not are_ptype_disjoint(h1.type, h2.type)
    ];

    errs = rel_var_errs & foreign_key_errs & method_errs & updates_errs & handler_errs;
    return [error_cluster(database, errs) if errs != []];
  }


  [CoreWFError] rel_var_errors(RelVar rel_var, [+NeType+] signatures, [[+Nat]] keys, Bool symmetric) {
    arities = [|s| : s <- signatures];
    return [multiple_rel_var_arities(rel_var, arities)] if |arities| != 1;
    return [
      overlapping_rel_var_signatures(rel_var, s1, s2)
      : s1, s2 <- symmetric_cart_prod(signatures), not are_ptype_disjoint(s1, s2)
    ];
  }


  [CoreWFError] foreign_key_errors(Database database, ForeignKey key) {
    src_rel = left(key.source);
    src_rel_info = database.rel_vars(src_rel);
    src_args, src_args_types = unzip(right(key.source));

    fail if src_args_types == ();
    src_sgns = [s : s <- src_rel_info.signatures, not are_disjoint(s, src_args_types)];
    return [no_signature_match(key, src_rel, src_rel_info.signatures, src_args_types)] if src_sgns == [];
    aggr_sgns = aggregated_elements(src_sgns, aggregated_signature);
    return [multiple_signature_matches(key, src_rel, src_sgns, src_args_types)] if |aggr_sgns| != 1;
    sgn = only(aggr_sgns);
    if not match_exactly(sgn, src_args_types):
      return [no_exact_signature_match(key, src_rel, sgn, src_args_types)];
    ;

    tgt_rel = left(key.target);
    tgt_rel_info = database.rel_vars(tgt_rel);
    tgt_args = right(key.target);

    for src_sgn <- isort(src_sgns):
      // Types of all variables when the key is applied to this specific signature of the source relation
      src_type_map = [value(a) -> src_sgn(i) : a @ i <~ src_args, a != nothing];
      tgt_args_types = (apply(a, src_type_map($)) : a <- tgt_args);
      // Signatures of the target that are not disjoint with the image of the source signature
      tgt_sgns = [s : s <- tgt_rel_info.signatures, not are_disjoint(s, tgt_args_types)];
      if tgt_sgns == []:
        return [no_signature_match(key, tgt_rel, tgt_rel_info.signatures, tgt_args_types)];
      elif |tgt_sgns| != 1:
        ## REALLY BAD ERROR MESSAGE. FIX
        return [multiple_signature_matches(key, tgt_rel, tgt_sgns, tgt_args_types)];
      ;
      tgt_sgn = only(tgt_sgns);
      if not is_subsignature(tgt_args_types, tgt_sgn):
        ## REALLY BAD ERROR MESSAGE. FIX
        return [no_exact_signature_match(key, tgt_rel, tgt_sgn, tgt_args_types)];
      ;
    ;

    is_supported = match (src_args, tgt_args)
      // unary -> unary
      (just()),                     (just())                    = true,

      // unary -> binary
      (just()),                     (just(),  nothing)          = true,
      (just()),                     (nothing, just())           = true,

      // unary -> ternary
      (just()),                     (just(), nothing, nothing)  = true,
      (just()),                     (nothing, just(), nothing)  = true,
      (just()),                     (nothing, nothing, just())  = true,

      // binary -> unary
      (just(), nothing),            (just())                    = true,
      (nothing, just()),            (just())                    = true,

      // ternary -> unary
      (just(), nothing, nothing),   (just())                    = true,
      (nothing, just(), nothing),   (just())                    = true,
      (nothing, nothing, just()),   (just())                    = true,

      // binary -> ternary
      (just(s1?), just(s2?)), (just(t1?), just(t2?), nothing)   = s1 == t1 and s2 == t2,

      // ternary -> binary
      (just(s1?), just(s2?), nothing),  (just(t1?), just(t2?))  = s1 == t1 and s2 == t2,

      _,                        _                               = false;

    return [unsupported_foreign_key_type(key)] if not is_supported;

    ## ARE THESE THE ONLY CASES WHERE WE EXPECT TABLES TO HAVE MATCHING SYMMETRIES?
    symmetry_is_fine = match (src_args, tgt_args)
      (just(), just()),           (just(), just(), nothing) = src_rel_info.symmetric == tgt_rel_info.symmetric,
      (just(), just(), nothing),  (just(), just())          = src_rel_info.symmetric == tgt_rel_info.symmetric,
      _,                          _                         = true;

    return [foreign_key_symmetry_mismatch(key)] if not symmetry_is_fine;

    ## IS THERE ANYTHING ELSE I'M SUPPOSED TO CHECK HERE?

    return [];


    Maybe[NeType+] aggregated_signature(NeType+ types1, NeType+ types2) {
      assert |types1| == |types2|;
      diff_idx = :none;
      for t1 @ i <- types1:
        t2 = types2(i);
        if not is_eq(t1, t2):
          return nothing if diff_idx != :none;
          diff_idx = i;
        ;
      ;
      // Works fine even if <diff_idx == :none>
      return just((if i != diff_idx then t else ne_union_type([t, types2(i)]) : t @ i <- types1));
    }


    // ## THESE ARE NOT SUPPOSED TO COMPILE, BUT THEY DO...
    // Bool are_disjoint(NeType+ types1, Maybe[NeType]+ types2)    = any(types2, types1, are_ptype_disjoint);
    // Bool match_exactly(NeType+ types1, Maybe[NeType]+ types2)   = all(types2, types1, is_eq);
    // Bool is_subsignature(Maybe[NeType]+ types2, NeType+ types1) = all(types2, types1, is_subset);


    Bool are_disjoint(NeType+ types1, Maybe[NeType]+ types2) {
      assert |types1| == |types2|;
      for t1 @ i <- types1:
        t2 = types2(i);
        return true if t2 != nothing and are_ptype_disjoint(t1, value(t2));
      ;
      return false;
    }


    Bool match_exactly(NeType+ types1, Maybe[NeType]+ types2) {
      assert |types1| == |types2|;
      for t1 @ i <- types1:
        t2 = types2(i);
        return false if t2 != nothing and not is_eq(t1, value(t2));
      ;
      return true;
    }


    Bool is_subsignature(Maybe[NeType]+ types2, NeType+ types1) {
      assert |types1| == |types2|;
      for t1 @ i <- types1:
        t2 = types2(i);
        return false if t2 != nothing and not is_subset(value(t2), t1);
      ;
      return true;
    }
  }

  //////////////////////////////////////////////////////////////////////////////

  [CoreWFError] automata_errors {
    auto_errs = union([automaton_errors(automaton=a) : a <- program.auto_defs]);

    shallow_deps = [auto.name -> [a.type : unused_var, a <- auto.nested_autos] : auto <- program.auto_defs];
    deep_deps = transitive_closure(shallow_deps);
    rec_errs = [recursively_defined_auto_or_db(automaton_by_id(auto_type)) : auto_type, deps <- deep_deps, deps(auto_type)];

    return auto_errs & rec_errs;
  }
}

////////////////////////////////////////////////////////////////////////////////

implicit program : Program, typedefs : [TypeName -> NeType], automaton : Automaton {
  [CoreWFError] automaton_errors {
    overlap_errs = union([
      [overlapping_signatures(m1, m2) : m1, m2 <- symmetric_cart_prod(ms), overlap(m1, m2)]
      : unused_var, ms <- group_by(automaton.methods, ($.name, arity($)))
    ]);
    var_errs = union(union([
      [param_errs(id, info.value) : id, info <- automaton.params],
      [state_var_errs(id, state_var) : id, state_var <- automaton.state],
      [rule_errs(id, expr) : id, expr <- automaton.rules],
      [time_rule_errs(id, rule) : id, rule <- automaton.time_rules],
      [discrete_rules_errs(id, rules) : id, rules <- automaton.discrete_rules],
      [nested_auto_errs(id, info.type, info.args) : id, info <- automaton.nested_autos],
      [nested_db_errs(id, info.msg_send_rule) : id, info <- automaton.nested_dbs, info.msg_send_rule?]
    ]));
    errs = overlap_errs & var_errs & output_errs;
    errs = dependency_errors if errs == [];
    return if errs != [] then [error_cluster(automaton, errs)] else [];


    [CoreWFError] output_errs {
      candidate_outputs = merge([
        [id -> false : id, unused_var <- automaton.params],
        [id -> input.discrete : id, input <- automaton.inputs],
        [id -> false : id, unused_var <- automaton.state],
        [id -> false : id, unused_var <- automaton.rules],
        [id -> false : id, unused_var <- automaton.time_rules],
        [id -> true : id, unused_var <- automaton.discrete_rules]
      ]);

      return [
        if candidate_outputs(id, *) then wrong_output_nature(id, output.discrete) else undefined_output(id)
        : id, output <- automaton.outputs, not candidate_outputs(id, *) or candidate_outputs[id] != output.discrete
      ];
    }

    [CoreWFError] param_errs(SignalVar id, Expr value) = [
      invalid_auto_var(v, id, value) : v <- auto_expr_extern_vars(value), not is_key(v, automaton.params)
    ];


    [CoreWFError] state_var_errs(SignalVar id, StateVar state_var) {
      ## HERE I SHOULD ALSO CHECK THAT THE UPDATES ARE NOT GUARANTEED TO FIRE TOGETHER...
      init_vars = continuous_signals_level_1;
      init_errs = [
        invalid_auto_var(v, id, state_var.init_value)
        : v <- auto_expr_extern_vars(state_var.init_value),
          not (v :: QualMembVar or in(v, init_vars))
      ];
      update_errs = union([state_var_update_errs(id, u) : u <- state_var.updates]);
      return init_errs & update_errs;
    }


    [CoreWFError] state_var_update_errs(SignalVar id, StateVarUpdate update) {
      continuous_signals = continuous_signals_level_2;
      delayed_signals = delayed_signals;
      discrete_signals = enabled_discrete_signals(update.cond);
      // Condition expression check
      cond_expr_vars = auto_expr_extern_vars(update.cond.cond);
      cond_expr_errs = [
        invalid_auto_var(v, id, update.cond.cond)
        : v <- cond_expr_vars,
          not (v :: QualMembVar or in(v, continuous_signals) or in(v, discrete_signals))
      ];
      // Value expression check
      value_expr_vars = auto_expr_extern_vars(update.value);
      value_expr_errs = [
        invalid_auto_var(v, id, update.value)
        : v <- cond_expr_vars,
          not (v :: QualMembVar or in(v, continuous_signals) or in(v, delayed_signals) or in(v, discrete_signals))
      ];
      return cond_expr_errs & value_expr_errs;
    }


    [CoreWFError] rule_errs(SignalVar id, Expr value) {
      signals = continuous_signals_level_2;
      return [
        invalid_auto_var(v, id, value)
        : v <- auto_expr_extern_vars(value), not (v :: QualMembVar or in(v, signals))
      ];
    }


    [CoreWFError] time_rule_errs(SignalVar id, TimeRule rule) {
      continuous_signals = continuous_signals_level_2;
      return match (rule)
        boolean_time_rule()   |
        unchanged_time_rule() = [ invalid_auto_var(v, id, rule.expr)
                                  : v <- auto_expr_extern_vars(rule.expr),
                                    not (v :: QualMembVar or in(v, continuous_signals))
                                ],
        since_time_rule()     = [invalid_auto_var(v, id, v) : v <- rule.signals, not is_discrete(v)];
    }


    // [CoreWFError] discrete_rules_errs(SignalVar id, [+DiscreteRule] rules) {
    ## RESTORE THE ABOVE SIGNATURE ONE THE TYPECHECKING BUG HAS BEEN FIXED
    [CoreWFError] discrete_rules_errs(SignalVar id, [DiscreteRule] rules) {
      ## HERE I SHOULD ALSO CHECK THAT THE RULES ARE NOT GUARANTEED TO FIRE TOGETHER...
      continuous_signals = continuous_signals_level_2;
      return [
        invalid_auto_var(v, id, e)
        : r <- rules,
          discrete_signals = enabled_discrete_signals(r.cond),
          e <- [r.cond.cond, r.value], v <- auto_expr_extern_vars(e),
          not (v :: QualMembVar or in(v, continuous_signals) or in(v, discrete_signals) or in(v, delayed_signals))
      ];
    }


    [CoreWFError] nested_auto_errs(NestedAutoVar id, AutoSymbol type, [Atom -> Expr] args) {
      nested_auto = automaton_by_id(type);
      exp_inputs = [bare_id(v) : v, unused_var <- nested_auto.inputs];
      errs  = [invalid_input(id, type, input_id) : input_id, unused_var <- args, not exp_inputs(input_id)] &
              [undefined_input(id, type, input_id) : input_id <- exp_inputs, not args(input_id, *)];
      return errs if errs != [];
      return union([{
          input_is_discrete = nested_auto.inputs[signal_var(input_id)].discrete;
          discrete_vars = [v : v <- auto_expr_extern_vars(e), is_discrete(v)];
          return if input_is_discrete
            then [continous_expr_for_discrete_input(id, input_id) if discrete_vars == []]
            else [invalid_nested_auto_input_var(v, id, input_id, e) : v <- discrete_vars];
        } : input_id, e <- args
      ]);
    }


    [CoreWFError] nested_db_errs(NestedDBVar id, MsgSendRule rule) {
      continuous_signals = continuous_signals_level_2;
      discrete_signals = enabled_discrete_signals(rule.cond);
      return [
        invalid_nested_db_msg_var(v, id, rule)
        : e <- [rule.cond.cond, rule.message], v <- auto_expr_extern_vars(e),
          not (v :: QualMembVar or in(v, continuous_signals) or in(v, discrete_signals))
      ];
    }
  }

  //////////////////////////////////////////////////////////////////////////////

  [CoreWFError] dependency_errors {
    shallow_deps = merge([
      [id -> dependencies(info.value) : id, info <- automaton.params],
      [id -> dependencies(id, sv) : id, sv <- automaton.state],
      [id -> dependencies(e) : id, e <- automaton.rules],
      [id -> dependencies(r) : id, r <- automaton.time_rules],
      [id -> union([dependencies(r) : r <- rs]) : id, rs <- automaton.discrete_rules],
      [id -> union([dependencies(e) : unused_var, e <- info.args]) : id, info <- automaton.nested_autos],
      [id -> dependencies(db.msg_send_rule) : id, db <- automaton.nested_dbs, db.msg_send_rule?]
    ]);
    deep_deps = transitive_closure(shallow_deps);
    return [dependency_loop(v, nonempty(ds)) : v, ds <- deep_deps, ds(v)];


    [SignalVar]     dependency(SignalVar var)       = [var];
    [NestedAutoVar] dependency(QualSignalVar var)   = [cast_nested_auto_var(var.auto_var)];
    [NestedDBVar]   dependency(QualMembVar var)     = [cast_nested_db_var(var.db_var)];
    []              dependency(PrevVarValue)        = [];


    [<SignalVar, NestedAutoVar, NestedDBVar>] dependencies(Expr expr) =
      union([dependency(v) : v <- auto_expr_extern_vars(expr)]);


    [<SignalVar, NestedAutoVar, NestedDBVar>] dependencies(ReactCond cond) {
      signals_deps = match (cond)
        react_cond()            = union([dependency(v) : v, unused_var <- cond.signals_state]),
        on_becoming_true_cond() = [];
      return dependencies(cond.cond) & signals_deps;
    }


    [<SignalVar, NestedAutoVar, NestedDBVar>] dependencies(SignalVar id, StateVar state_var) {
      init_deps = dependencies(state_var.init_value);
      update_deps = union([dependencies(u.value) & dependencies(u.cond) : u <- state_var.updates]) - [id];
      return init_deps & update_deps;
    }


    [<SignalVar, NestedAutoVar, NestedDBVar>] dependencies(TimeRule rule) =
      boolean_time_rule()   |
      unchanged_time_rule() = dependencies(rule.expr),
      since_time_rule()     = rule.signals;


    [<SignalVar, NestedAutoVar, NestedDBVar>] dependencies(DiscreteRule rule) =
      dependencies(rule.value) & dependencies(rule.cond);


    [<SignalVar, NestedAutoVar, NestedDBVar>] dependencies(MsgSendRule rule) =
      dependencies(rule.message) & dependencies(rule.cond);
  }

  //////////////////////////////////////////////////////////////////////////////
  //////////////////////////////////////////////////////////////////////////////

  [AnySignalVar] enabled_discrete_signals(ReactCond cond) =
    on_becoming_true_cond() = [],
    react_cond()            = [v : v, s <- cond.signals_state, s, is_discrete(v)];

  //////////////////////////////////////////////////////////////////////////////
  //////////////////////////////////////////////////////////////////////////////

  [SignalVar] continuous_signals_level_1 =
    keys(automaton.params) &
    [id : id, input <- automaton.inputs, not input.discrete] &
    keys(automaton.state) &
    keys(automaton.rules);

  [AnySignalVar] continuous_signals_level_2 =
    continuous_signals_level_1 &
    keys(automaton.time_rules) &
    [ qual_signal_var(id, out_id)
      : id, info <- automaton.nested_autos,
        out_id, out_info <- automaton_by_id(info.type).outputs,
        not out_info.discrete
    ];

  [<AnySignalVar, PrevVarValue>] delayed_signals =
    [prev_var_value(id) : id, input <- automaton.inputs, not input.discrete] &
    [prev_var_value(id) : id, unused_var <- automaton.state];
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

## THE IMPLICIT ARGUMENTS ARE TOTALLY UNNECESSARY, BUT THE TYPECHECKER IS TO DUMB TO FIGURE THAT OUT
implicit program : Program, automaton : Automaton {
  Bool is_discrete(<PrevVarValue, QualMembVar>) = false;
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit typedefs : [TypeName -> NeType] {
  Bool overlap(<Method, Update> d1, <Method, Update> d2) =
    none((are_ptype_disjoint(left(a1), left(a2)) : a1, a2 <- zip(d1.args, d2.args)));
}

T attach(T obj, Any data) = undefined; // Overridden in the generated code

Maybe[Any] fetch(T obj) = undefined;   // Overridden in the generated code

A transfer(A to, B from) =
  match (fetch(from))
    just(a?)  = attach(to, a),
    nothing   = to;

////////////////////////////////////////////////////////////////////////////////

String* format_error_msgs(Any decl, String*+ msgs) {
  merged_msgs = join(intermix((indent_lines(m) : m <- msgs), ("")));
  fname, sl, el = source_code_location(decl);
  loc_msg = "File " & fname & " line" & if sl == el then " " & printed(sl) else "s " & printed(sl) & "-" & printed(el);
  return (loc_msg) & merged_msgs;
}

////////////////////////////////////////////////////////////////////////////////

String source_file(Any decl) = source_code_location(decl)(0);

NzNat source_line(Any decl) = source_code_location(decl)(1);

(String, NzNat, NzNat) source_code_location(Any decl) {
  maybe_data = fetch(decl);
  if maybe_data != nothing:
    data = value(maybe_data);
    return data if data :: (String, NzNat, NzNat);
  ;
  err_msg = "CANNOT DETERMINE LOCATION OF OFFENDING CODE";
  err_msg = err_msg & ": " & printed(value(maybe_data)) if maybe_data != nothing;
  return (err_msg, 1000000, 1000000);
}


// (String, NzNat, NzNat) source_code_location(<PrgDecl, DBMethodDef, UpdateDef, ImplArgDecl> decl_or_sgn) {
//   maybe_data = fetch(decl_or_sgn);
//   if maybe_data != nothing:
//     data = value(maybe_data);
//     if data :: (String, NzNat, NzNat):
//       return data;
//     else
//       print data;
//     ;
//   ;
//   return ("Unknown source file", 1, 1);
// }

////////////////////////////////////////////////////////////////////////////////

(String, NzNat) file_and_line(Maybe[Any] maybe_decl_loc, Maybe[Any] maybe_err_loc) {
  file = "FILE UNKNOWN";
  line = 1000000000;

  err_code = ();
  if maybe_decl_loc != nothing:
    decl_loc = value(maybe_decl_loc);
    if decl_loc :: (String, NzNat, NzNat):
      file, line, unused_var = decl_loc;
    ;
  ;

  if maybe_err_loc != nothing:
    err_loc = value(maybe_err_loc);
    if err_loc :: ((NzNat, NzNat), (NzNat, NzNat)):
      line = err_loc(0)(0);
    ;
  ;

  return (file, line);
}

////////////////////////////////////////////////////////////////////////////////

String* indent_lines(String* strs) = (if s != "" then "  " & s else "" : s <- strs);

////////////////////////////////////////////////////////////////////////////////

Nat uppercase(Nat ch) = if ch >= ascii_lower_a and ch <= ascii_lower_z then nat(ch - ascii_lower_a + ascii_upper_a) else ch;


String uppercase(Atom a) = string((uppercase(ch) : ch <- chars(_print_(a))));


String sentence_case(Atom a) = string((if i == 0 then uppercase(ch) else ch : ch @ i <- chars(_print_(a))));


String upper_camel_case(Atom a) = capitalize(_print_(a), false);


String capitalize(String s, Bool keep_underscores) {
  first = true;
  res = ();
  for ch @ i <- chars(s):
    tr_ch = ch;
    if ch == ascii_underscore:
      first = true;
    else
      tr_ch  = nat(ch - ascii_lower_a + ascii_upper_a) if ch >= ascii_lower_a and ch <= ascii_lower_z and first; ## CREATE PRELUDE FUNCTION is_lower()/is_upper() AND to_lower()/to_upper()
      first = false;
    ;
    res = res & (tr_ch if tr_ch != ascii_underscore or keep_underscores);
  ;
  return string(res);
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

String user_repr(Type type) = user_repr(type, false, false, false);

String user_repr(Type type, Bool inside_type_union, Bool incl_empty_seq, Bool incl_empty_rel) =
  empty_type                = "</>",
  atom_type                 = "<+>",
  symb_type(object(a?))     = {res = _print_(a); res = "<" & res & ">" if not inside_type_union; return res;},
  integer                   = "<*..*>",
  low_ints()                = "<*.." & printed(type.max) & ">",
  high_ints()               = "<" & printed(type.min) & "..*>",
  int_range()               = "<" & printed(type.min) & ".." & printed(type.min + type.size - 1) & ">",
  float_type                = "<!>",
  empty_seq_type            = "()",
  empty_rel_type            = "[]",
  type_var(<+> a?)          = {str = upper_camel_case(a); str = str & "'" if length(str) > 1; return str;},
  type_var(<*..*> n?)       = "<" & printed(n) & "'>",
  type_ref(ts?)             = user_repr(ts),
  ne_seq_type()             = user_repr(type.elem_type) & if incl_empty_seq then "*" else "+",
  ne_set_type()             = "[" & {if incl_empty_rel then "" else "+"} & user_repr(type.elem_type) & "]",
  ne_map_type()             = "[" & {if incl_empty_rel then "" else "+"} &
                              user_repr(type.key_type) & " -> " & user_repr(type.value_type) & "]",
  ne_bin_rel_type()         = "[" & {if incl_empty_rel then "" else "+"} &
                              append((append((user_repr(t) : t <- s), ", ") : s <- rand_sort(type.signatures)), "; ") & "]",
  ne_tern_rel_type()        = "[" & {if incl_empty_rel then "" else "+"} &
                              append((append((user_repr(t) : t <- s), ", ") : s <- rand_sort(type.signatures)), "; ") & "]",
  ne_record_type(fs?)       = ne_record_type_user_repr(fs),
  tuple_type(ts?)           = "(" & append((user_repr(t) : t <- ts), ", ") & ")",
  tag_obj_type()            = tag_obj_type_user_repr(type.tag_type, type.obj_type),
  union_type(ts?)           = union_type_user_repr(ts);


String ne_record_type_user_repr(RecordTypeFields fields) {
  field_reprs = [record_field_user_repr(l, f.type, f.optional) : l, f <- fields];
  return "(" & append(rand_sort(field_reprs), ", ") & ")";

  String record_field_user_repr(SymbObj label, NeType type, Bool optional) =
    _print_(match (label) object(a?) = a) & ": " & user_repr(type) & if optional then "?" else "";
}


String union_type_user_repr([+NeType] types) {
  incl_empty_seq = in(empty_seq_type, types);
  incl_empty_rel = in(empty_rel_type, types);

  types_to_exclude = [
    empty_seq_type if (t <- types : match (t) ne_seq_type() = true, _ = false),
    empty_rel_type if (t <- types : repr_includes_empty_rel(t))
  ];

  types_left_sorted = rand_sort(types - types_to_exclude);
  is_still_union = |types_left_sorted| > 1;
  types_reprs = (
    user_repr(t, is_still_union, incl_empty_seq, incl_empty_rel)
    : t <- types_left_sorted
  );
  repr_str = append(types_reprs, ", ");
  repr_str = "<" & repr_str & ">" if is_still_union;
  return repr_str;


  Bool repr_includes_empty_rel(NeType type) =
    ne_set_type()         |
    ne_map_type()         |
    ne_bin_rel_type()     |
    ne_tern_rel_type()    = true,
    ne_record_type(fs?)   = not (l, f <- fs : not f.optional),
    _                     = false;
}


String tag_obj_type_user_repr(TagType tag_type, NeType obj_type) {
  return "String" if tag_type == symb_type(:string) and is_nat_seq_type(obj_type);

  skip_pars = match (obj_type)
    tuple_type() | ne_record_type()   = true,
    _                                 = false;

  tag_repr = match (tag_type)
    atom_type               = "<+>",
    symb_type(object(tag?)) = _print_(tag);

  obj_repr = user_repr(obj_type);
  obj_repr = "(" & obj_repr & ")" if not skip_pars;

  return tag_repr & obj_repr;


  Bool is_nat_seq_type(NeType type) =
    empty_seq_type    = true,
    ne_seq_type()     = is_nat_type(type.elem_type),
    tuple_type(ts?)   = all((is_nat_type(t) : t <- ts)),
    union_type(ts?)   = not (t <- ts : not is_nat_seq_type(t)),
    _                 = false;


  Bool is_nat_type(NeType type) =
    integer           = false,
    low_ints()        = false,
    high_ints()       = type.min >= 0,
    int_range()       = type.min >= 0,
    union_type(ts?)   = not (t <- ts : not is_nat_type(t)),
    _                 = false;
}


String user_repr(TypeSymbol type_symb) =
  type_symbol(a?)           |
  schema_type_symbol(a?)    |
  builtin_type_symbol(a?)   = upper_camel_case(a),
  par_type_symbol()         = user_repr(type_symb.symbol) & "[" & append((user_repr(p) : p <- type_symb.params), ", ") & "]";


String user_repr(TypeName type_name) {
  repr = user_repr(type_name.symbol);
  repr = repr & "/" & printed(type_name.arity) if type_name.arity != 0;
  return repr;
}


String user_repr(SymbObj) =
  object(a?)  = _print_(a);


String user_repr(Var var) =
  var(a?)               = _print_(a),
  fn_arg(n?)            = "$" & printed(n),
  impl_arg(a?)          = _print_(a),
  memb_var(a?)          = _print_(a),
  signal_var(a?)        = _print_(a),
  prev_var_value(v?)    = "@" & user_repr(v),
  qual_memb_var(a?)     = user_repr(var.db_var) & "." & user_repr(var.memb_var),
  qual_signal_var(a?)   = user_repr(var.auto_var) & "." & user_repr(var.memb_var);


String user_repr(AnyAutoVar v) = _print_(bare_id(v));


String user_repr(AnyDBVar v) =
  loc_db_var(a?)    |
  nested_db_var(a?) = _print_(a),
  qual_db_var()     = user_repr(v.outer) & "." & user_repr(v.inner);


String user_repr(AnyRelVar v) =
  rel_var(a?)     = _print_(a),
  qual_rel_var()  = user_repr(v.db_var) & "." & user_repr(v.rel_var);


String user_repr(UniqueRelVar) =
  unique_rel_var(s?, _)   = _print_(s);


String user_repr(ProcSymbol) =
  proc_symbol(a?)   = upper_camel_case(a);


String user_repr(BuiltIn b) = "_" & _print_(b) & "_";


String user_repr(PlainFnFamilyId id)  = user_repr(id.symbol, id.arity);
String user_repr(UniqueFnId id)       = user_repr(id.family);
String user_repr(NestedFnId id)       = user_repr(id.outer) & ":" & user_repr(id.inner);


String user_repr(FnSymbol fn_symb, Nat arity) =
  fn_symbol(a?)         = _print_(a) & "/" & printed(arity),
  op_symbol(op?)        = user_repr(op);


String user_repr(PlainAggrFnId id) = aggr_fn_id(s?) = _print_(s);


String user_repr(ConvFnId) = undefined;


String user_repr(ClsVar v) =
  cls_var(<+> a?)    = _print_(a);


String user_repr(ClsType type) = cls_type_user_repr(type.in_types, type.out_type);

String cls_type_user_repr(Type+ arg_types, Type ret_type) =
  "(" & append((user_repr(t) : t <- arg_types), ", ") & " -> " & user_repr(ret_type) & ")";

String user_repr(FnType type) = append((user_repr(t) : t <- type.args), ", ") & " -> " & user_repr(type.ret_type);

String user_repr(AggrFnType type) {
  args_reprs = (user_repr(type.elt_type) & "..",) & (user_repr(t) : t <- type.extra_args_types);
  return append(args_reprs, " ") & " -> " & user_repr(type.ret_type);
}

## BUG BUG BUG: THIS IS PROBABLY ALL WRONG...
String user_repr(Pattern ptrn) =
  ptrn_symbol         = "<+>",
  ptrn_float          = "<!>",
  ptrn_empty_seq      = "()",
  ptrn_ne_seq         = "()",
  ptrn_empty_rel      = "[]",
  ptrn_ne_set         = "[]",
  ptrn_ne_map         = "[->]",
  ptrn_ne_bin_rel     = "[,]",
  ptrn_ne_tern_rel    = "[,,]",
  ptrn_any            = "_",
  ptrn_symbol(s?)     = _print_(match (s) object(a?) = a),
  ptrn_integer(t?)    = user_repr(t),
  ptrn_tuple(ps?)     = "(" & append((user_repr(p) : p <- ps), ", ") & ")",
  ptrn_tag_obj()      = ptrn_tag_obj_user_repr(ptrn.tag, ptrn.obj),
  ptrn_var()          = {if ptrn.ptrn != ptrn_any then user_repr(ptrn.ptrn) & " " else ""} & user_repr(ptrn.var) & "?",
  ptrn_union(ps?)     = append((user_repr(p) : p <- rand_sort(ps)), " | ");


String ptrn_tag_obj_user_repr(TagPtrn tag, Pattern obj) =
  ptrn_symbol               = "_ @ " & user_repr(obj),
  ptrn_symbol(object(s?))   = _print_(s) & "(" & user_repr(obj) & ")",
  ptrn_var()                = user_repr(tag.var) & " @ " & user_repr(obj);


String user_repr(FnSymbol fn_symb) =
  fn_symbol(a?)         = _print_(a),
  op_symbol(op?)        = user_repr(op);


String user_repr(<MethodSymbol, UpdateSymbol> symb, Nat arity) = user_repr(symb) & "/" & printed(arity);


String user_repr(MethodSymbol symb) = method_symbol(a?)  = _print_(a);

String user_repr(UpdateSymbol symb) = update_symbol(a?)  = _print_(a);

String user_repr(AutoSymbol) = auto_symbol(a?) = upper_camel_case(a);

String user_repr(DBSymbol) = db_symbol(a?) = upper_camel_case(a);


String user_repr(Operator) =
  unary_minus   = "(-_)",
  plus          = "(_+_)",
  minus         = "(_-_)",
  star          = "(_*_)",
  slash         = "(_/_)",
  exp           = "(_^_)",
  amp           = "(_&_)",
  lower         = "(_<_)",
  greater       = "(_>_)",
  lower_eq      = "(_<=_)",
  greater_eq    = "(_>=_)",
  brackets      = "(_[_])";


String user_repr(ForeignKey key) {
  src_args_list = (
    match (v)
      just(s?)  = printed(s),
      nothing   = "_"
    : v @ i <- key.source_args
  );
  tgt_args_list = (if a != nothing then printed(value(a)) else "_" : a <- key.target_args);
  return user_repr(key.source_rel_var) & "(" & append(src_args_list, ", ") & ") -> " &
         user_repr(an_elem(key.target_rel_vars)) & "(" & append(tgt_args_list, ", ") & ")";
}

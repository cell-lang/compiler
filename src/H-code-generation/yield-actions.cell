type YieldValueAction   = yield_value_action(var: AnyLocVar, expr: Expr, instrs: Instr+);

type SetYieldAction     = set_yield_action(var: StdVar, src_set: Expr, next: YieldAction);
type RelYieldAction     = rel_yield_action(
                            vars: StdVar+, src_rel: Expr, args: Maybe[Expr]+, next: YieldAction
                          );

type RelVarYieldAction  = relvar_yield_action(
                            vars: StdVar+, relvar: AnyRelVar, args: Maybe[Expr]+, next: YieldAction
                          );

type SeqYieldAction     = seq_yield_action(
                            vars: Maybe[StdVar]+, idx_var: Maybe[StdVar], src_seq: Expr, next: YieldAction
                          );

type MatchYieldAction   = match_yield_action(ptrn: Pattern, expr: Expr, next: YieldAction);
type LetYieldAction     = let_yield_action(var: StdVar, expr: Expr, next: YieldAction);
type CondYieldAction    = cond_yield_action(cond: Expr, next: YieldAction);

type YieldActions       = yield_actions(YieldAction+);

type YieldAction        = YieldValueAction, SetYieldAction, RelYieldAction, RelVarYieldAction, SeqYieldAction,
                          MatchYieldAction, LetYieldAction, CondYieldAction, YieldActions;

////////////////////////////////////////////////////////////////////////////////

YieldValueAction yield_value_action(AnyLocVar v, Expr e, Instr+ is) =
  yield_value_action(var: v, expr: e, instrs: is);

CondYieldAction cond_yield_action(Expr c, YieldAction a) = cond_yield_action(cond: c, next: a);

RelVarYieldAction relvar_yield_action(StdVar+ vs, AnyRelVar r, Maybe[Expr]+ as, YieldAction a) =
  relvar_yield_action(vars: vs, relvar: r, args: as, next: a);

YieldAction yield_actions(YieldAction+ actions) =
  if |actions| != 1 then :yield_actions(actions) else only(actions);

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

YieldAction yield_action(Clause cls, YieldAction next) =
  set_clause()      = set_yield_action(var: cls.var, src_set: cls.src, next: next),
  rel_clause()      = rel_yield_action(vars: cls.vars, src_rel: cls.src, args: cls.args, next: next),
  relvar_clause()   = relvar_yield_action(vars: cls.vars, relvar: cls.rel_var, args: cls.args, next: next),
  seq_clause()      = seq_yield_action(vars: cls.vars, idx_var: cls.idx_var, src_seq: cls.src, next: next),
  and_clause()      = yield_action(cls.left, yield_action(cls.right, next)),
  or_clause()       = yield_actions((yield_action(cls.left, next), yield_action(cls.right, next))),
  match_clause()    = match_yield_action(ptrn: cls.ptrn, expr: cls.expr, next: next),
  asgnm_clause()    = let_yield_action(var: cls.var, expr: cls.expr, next: next),
  filter_clause(e?) = cond_yield_action(cond: e, next: next);

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit program           : Program,
         typedefs          : [TypeName -> NeType],
         context           : Context,
         cluster           : Expr+,
         types             : Type+,
         environments      : [EnvVar -> Type]+,
         surrogates        : [SurrExpr -> ObjExpr],
         expr_id           : Nat,
         options           : CompOptions {

  Instr* yield_action_opt_code(YieldAction action, VarGen var_gen) =
    yield_value_action()    = yield_value_action_opt_code(action, var_gen),
    set_yield_action()      = set_yield_action_opt_code(action, var_gen),
    rel_yield_action()      = rel_yield_action_opt_code(action, var_gen),
    relvar_yield_action()   = if is_columnar_relvar(action.relvar)
                                then columnar_relvar_yield_action_opt_code(action, var_gen)
                                else relvar_yield_action_opt_code(action, var_gen),
    seq_yield_action()      = seq_yield_action_opt_code(action, var_gen),
    match_yield_action()    = match_yield_action_opt_code(action, var_gen),
    let_yield_action()      = let_yield_action_opt_code(action, var_gen),
    cond_yield_action()     = cond_yield_action_opt_code(action, var_gen),
    yield_actions(as?)      = join((yield_action_opt_code(a, var_gen) : a <- as));


  // relvar_yield_action(vars: StdVar+, relvar: AnyRelVar, args: Maybe[Expr]+, next: YieldAction)
  Instr* columnar_relvar_yield_action_opt_code(RelVarYieldAction action, VarGen init_var_gen) {
    fail if not action.args :: (Maybe[ExprRef], Nothing) or |action.vars| != 2;

    if action.args(0) != nothing:
      fail; ## IMPLEMENT IMPLEMENT IMPLEMENT
    ;

    var_0, var_1 = action.vars;
    surr_var_0 = shadow_surr_var(var_0);

    // Selecting the candidate tables among those the logical relation is partitioned into
    // In this specific case, that means all of them
    candidate_tables = overlapping_tables(action.relvar, (nothing, nothing));

    code = ();

    // Iterating through all candidate tables
    for tv <- isort(candidate_tables):
      sign = table_signature(tv);
      repr_0 = low_level_repr(sign(0));
      repr_1 = low_level_repr(sign(1));
      ll_var_0 = low_level_var(repr_0, var_0);
      ll_var_1 = low_level_var(repr_1, var_1);

      store_id = table_col_store_id(tv, 0);
      store_type = value_store_type(rel_auto_type(action.relvar), unqualified(store_id));
      store_id = substitute(store_id);

      col_repr = column_repr(sign(1));
      it_var, var_gen = next_column_it_var(init_var_gen, col_repr);

      vars_setup_code = (
        set_ivar32(surr_var_0, get_col_index(it_var)),
        surr_to_decoded_value(ll_var_0, store_id, store_type, surr_var_0),
        set_any_var(ll_var_1, column_lookup(substitute(tv), surr_var_0, col_repr))
      );

      next_action_surrs = surrogates & [var_0 -> obj_expr(ll_var_0), var_1 -> obj_expr(ll_var_1)];
      action_code = yield_action_opt_code(action.next, var_gen, surrogates = next_action_surrs);

      loop_code = (break_if(is_out_of_range(it_var))) & vars_setup_code & action_code & (move_forward(it_var));

      table_code = (
        get_column_iter(it_var, substitute(tv)),
        repeat(nonempty(loop_code))
      );

      code = code & table_code;
    ;

    return code;
  }


  // relvar_yield_action(vars: StdVar+, relvar: AnyRelVar, args: Maybe[Expr]+, next: YieldAction)
  Instr* relvar_yield_action_opt_code(RelVarYieldAction action, VarGen init_var_gen) {
    args = action.args;
    set_args = values(args);

    bound_args_idxs = (i : a @ i <- args, a != nothing);
    free_args_idxs = (i : a @ i <- args, a == nothing);
    arg_idx_bound_arg_idx = [i -> j : i @ j <~ bound_args_idxs];

    args_eval_code, args_aliases, var_gen = opt_eval_info(set_args, init_var_gen);

    surr_vars, var_gen = next_int32_vars(var_gen, |set_args|);
    shadow_surr_vars, var_gen = next_int32_vars(var_gen, |action.vars|);
    it_var, var_gen = next_table_it_var(var_gen, |args|, symmetric(action.relvar));

    surr_args = (apply(a, surr_vars(arg_idx_bound_arg_idx(i))) : a @ i <- args);

old_filtered_idxs = idxs_into_filtered_seq((a != nothing : a <- args));
old_unfiltered_idxs = (i : a @ i <- args, a != nothing);
old_blank_idxs = (i : a @ i <- args, a == nothing);
old_surr_args = (apply(mi, surr_vars($)) : mi <- old_filtered_idxs);
assert bound_args_idxs == old_unfiltered_idxs;
assert free_args_idxs == old_blank_idxs;
assert surr_args == old_surr_args;

    symmetric = symmetric(action.relvar);

    // Selecting the candidate tables among those the logical relation is partitioned into
    args_types = (apply(a, subexpr_type) : a <- action.args);
    return (terminate(true)) if not args_types :: Maybe[NeType]+;
    candidate_tables = overlapping_tables(action.relvar, args_types);

    code = args_eval_code;

    // Iterating through all candidate tables
    for tv <- isort(candidate_tables):
      assert (a <~ surr_args : a == nothing);

      sign = table_signature(tv);
      vars_types = (sign(free_args_idxs(i)) : i < |action.vars|);
      low_level_vars = (low_level_var(low_level_repr(vars_types(i)), v) : v @ i <- action.vars);

      vars_setup_code = join(({
        shadow_surr_var = shadow_surr_vars(i);
        store_id = table_col_store_id(tv, free_args_idxs(i));
        store_type = value_store_type(rel_auto_type(action.relvar), unqualified(store_id));
        store_id = substitute(store_id);
        return (
          set_ivar32(shadow_surr_var, get_unknown_arg(it_var, i)),
          surr_to_decoded_value(v, store_id, store_type, shadow_surr_var)
        );
      } : v @ i <- low_level_vars));

      next_action_surrs = surrogates & [v -> obj_expr(low_level_vars(i)) : v @ i <~ action.vars];
      action_code = yield_action_opt_code(action.next, var_gen, surrogates = next_action_surrs);

      loop_code = (break_if(is_out_of_range(it_var))) & vars_setup_code & action_code & (move_forward(it_var));
      table_code = (
        get_table_iter(it_var, substitute(tv), nonempty(surr_args)),
        repeat(nonempty(loop_code))
      );

      for e @ i <- reverse(args_aliases):
assert |args_types| == |args|;
assert |args_aliases| == |set_args|;
assert |bound_args_idxs| == |set_args|;
assert not (j <~ bound_args_idxs : args(j) == nothing or args_types(j) == nothing);

        idx = rat(bound_args_idxs, i);
        arg_type = value_unsafe(args_types(idx));
        store_id = table_col_store_id(tv, idx);
        store_type = value_store_type(rel_auto_type(action.relvar), unqualified(store_id));
        store_id = substitute(store_id);
        surr_var = rat(surr_vars, i);
        table_code = opt_do_if(
          value_store_guard(store_type, e, arg_type),
          ( set_ivar32(surr_var, value_to_surr(store_id, value_store_encoded_expr(store_type, e))),
            do_if_not(is_eq(surr_var, -1), table_code)
          )
        );
      ;

      code = code & table_code;
    ;

    return code;
  }


  // rel_yield_action(vars: StdVar+, src_rel: Expr, args: Maybe[Expr]+, next: YieldAction)
  Instr* rel_yield_action_opt_code(RelYieldAction action, VarGen init_var_gen) {
    args_surrs : Maybe[ObjExpr]*;

    code, src_surr, var_gen = opt_eval_info(action.src_rel, init_var_gen);
    if |action.args| == 2:
      it_var, var_gen = next_bin_rel_it_var(var_gen);
    else
      assert |action.args| == 3;
      it_var, var_gen = next_tern_rel_it_var(var_gen);
    ;

    args_surrs = ();
    for a <- action.args:
      if a != nothing:
        arg_code, arg_surr, var_gen = opt_eval_info(value(a), var_gen);
        code = code & arg_code;
        args_surrs = (args_surrs | just(arg_surr));
      else
        args_surrs = (args_surrs | nothing);
      ;
    ;
    code = (code | get_any_rel_iter(it_var, src_surr, args_surrs));

    body = (break_if(is_out_of_range(it_var)));

    vars_idxs = (i : a @ i <- action.args, a == nothing);
    body = body & (get_curr_arg(v, it_var, vars_idxs(i)) : v @ i <- action.vars);

    body = body & yield_action_opt_code(action.next, var_gen);
    body = (body | move_forward(it_var));

    return (code | repeat(body));
  }


  // yield_value_action(var: LocVar, expr: Expr, instrs: Instr+)
  Instr* yield_value_action_opt_code(YieldValueAction action, VarGen var_gen) {
    code, surr, unused_var = opt_eval_info(action.expr, var_gen);
    code = store_result(code, surr, action.var);
    return code & action.instrs;
  }


  // set_yield_action(var: StdVar, src_set: Expr, next: YieldAction)
  Instr* set_yield_action_opt_code(SetYieldAction action, VarGen init_var_gen) {
    src_eval_code, src_surr, var_gen = opt_eval_info(action.src_set, init_var_gen);
    it_var, var_gen = next_set_it_var(var_gen);

    nested_code = yield_action_opt_code(action.next, var_gen);

    return src_eval_code & (
      get_iter(it_var, src_surr),
      repeat((
        break_if(is_out_of_range(it_var)),
        set_var(action.var, get_curr_obj(it_var))
        ) & nested_code & (
        move_forward(it_var)
      ))
    );
  }


  // seq_yield_action(vars: Maybe[StdVar]+, idx_var: Maybe[StdVar], src_seq: Expr, next: YieldAction)
  Instr* seq_yield_action_opt_code(SeqYieldAction action, VarGen init_var_gen) {
    // Calculating source type, and returning early if the type is trivial
    src_type = subexpr_type(action.src_seq);
    if src_type == empty_type:
      return (terminate);
    elif is_subset(src_type, empty_seq_type):
      return ();
    ;

    // Types of elements of source and destination sequences
    src_elt_type = seq_elem_supertype(src_type);
    elt_type = subexpr_type(action.src_seq);
    assert src_elt_type != empty_type and elt_type != empty_type;
    fail if src_elt_type == empty_type or elt_type == empty_type;

    // Low-level representation of elements of source and destination sequences
    src_repr = opt_repr(src_elt_type);

    // Evaluation of source expression
    eval_code, src_surr, var_gen = opt_eval_info(action.src_seq, init_var_gen);
    if not src_surr :: <LocVar, StdVar>:
      src_var, var_gen = next_obj_var(var_gen);
      eval_code = (eval_code | set_var(src_var, src_surr));
      src_surr = src_var;
    ;

    // Temporary buffer for source data
    src_array_var, var_gen = next_array_var(var_gen, src_repr);

    // Input sequence size variable
    array_size_var, var_gen = next_int32_var(var_gen);
    set_size_var = set_ivar32(array_size_var, get_size(src_surr));

    vars = action.vars;
    body = ();

    if |vars| == 1:
      var = only(vars); ## CHECK IF THIS IS ALWAYS VERIFIED...
      if var != nothing:
        var = value(var);
        it_var = low_level_var(src_repr, var);
        surrs = surrogates & [var -> obj_expr(it_var) if it_var != var];
      else
        ## THIS IS UNNECESSARY, BECAUSE THE VALUE IS NOT BEING USED,
        ## BUT SINCE IT'S SUCH A MARGINAL USE CASE...
        it_var, var_gen = next_obj_var(var_gen);
        surrs = surrogates;
      ;

    else
      assert src_repr == :object;
      vars_types = tuple_field_supertypes(src_elt_type, |vars|);
      ## REMOVE WHEN DONE
      assert vars_types == (restricted_seq_elem_supertype(src_elt_type, singleton_type(i)) : i < |vars|);
      reprs = (opt_repr(t) : t <- vars_types);
      it_var, var_gen = next_obj_var(var_gen);
      surrs = surrogates;
      for mv @ i <- vars:
        if mv != nothing:
          v = value(mv);
          phys_var = low_level_var(reprs(i), v);
          surrs = surrs & [v -> obj_expr(phys_var) if v != phys_var];
          body = (body | set_any_var(phys_var, at(it_var, i)));
        ;
      ;
    ;

    // Index variables
    hidden_idx_var, var_gen = next_int32_var(var_gen);
    surrs = surrs & [value(action.idx_var) -> int_obj(hidden_idx_var)] if action.idx_var != nothing;

    // Evaluation of nested yield action
    body = body & yield_action_opt_code(action.next, var_gen, surrogates = surrs);

    // Looping over the source sequence
    loop_instr = foreach(
      seq:        src_surr,
      src_array:  src_array_var,
      // tmp_buffer: array_var,
      count_var:  array_size_var,
      idx_var:    hidden_idx_var,
      it_var:     it_var,
      body:       body
    );

    return (eval_code | set_size_var, loop_instr);
  }


  // match_yield_action(ptrn: Pattern, expr: Expr, next: YieldAction)
  Instr* match_yield_action_opt_code(MatchYieldAction action, VarGen init_var_gen) {
    eval_code, expr_surr, var_gen = opt_eval_info(action.expr, init_var_gen);
    expr_type = subexpr_type(action.expr);

    ptrn_env = generate_env(action.ptrn, expr_type);
    vars_reprs = [v -> low_level_var(opt_repr(t), v) : v, t <- ptrn_env];
    surrs = surrogates & [v -> obj_expr(pv) : v, pv <- vars_reprs];

    let surrogates = surrs, vars_reprs = vars_reprs:
      nested_code = yield_action_opt_code(action.next, var_gen);
      code = opt_ptrn_match_code(action.ptrn, expr_surr, expr_type, nested_code);
    ;

    return eval_code & code;
  }


  // let_yield_action(var: StdVar, expr: Expr, next: YieldAction);
  Instr* let_yield_action_opt_code(LetYieldAction action, VarGen init_var_gen) {
    repr = opt_repr(subexpr_type(action.expr));
    phys_var = low_level_var(repr, action.var);
    eval_code, surr, var_gen = opt_eval_info(action.expr, init_var_gen);
    eval_code = store_result(eval_code, surr, phys_var);
    surrs = surrogates & [action.var -> obj_expr(phys_var) if action.var != phys_var];
    nested_code = yield_action_opt_code(action.next, var_gen, surrogates = surrs);
    return eval_code & nested_code;
  }


  // cond_yield_action(cond: Expr, next: YieldAction);
  Instr* cond_yield_action_opt_code(CondYieldAction action, VarGen init_var_gen) {
    eval_code, cond_surr, var_gen = opt_eval_info(action.cond, init_var_gen);
    nested_code = yield_action_opt_code(action.next, var_gen);
    return (eval_code | do_if(bool_value(cond_surr), nested_code));
  }
}

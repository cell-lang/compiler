implicit program : Program, typedefs : [TypeName -> NeType], database : Database, options : CompOptions {
  StaticBlock compiled_database {
    methods = compiled_database_methods(database.methods);
    updates = compiled_database_updates(database.updates);
    handlers = compiled_database_handlers(database.handlers);

    msg_types = [h.type : h <- database.handlers];
    msg_memb_pred = if msg_types != [] then just(memb_test_preds_map[ne_union_type(msg_types)]) else nothing;

    return static_block(
      name:             database.name,
      links:            database.links,
      state_vars:       [v -> info.type : v, info <- database.state_vars],
      value_stores:     value_stores,
      tables:           tables_info,
      foreign_keys:     physical_foreign_keys,
      nested_blocks:    database.nested_dbs,
      init_code:        database_init_code,
      cleanup_code:     database_cleanup_code,
      copy_state_code:  database_copy_state_code,
      set_state_code:   database_set_state_code,
      full_check_code:  database_full_check_code,
      methods:          methods,
      updates:          handlers & updates,
      msg_memb_pred:    msg_memb_pred,
      state_memb_pred:  memb_test_preds_map[type_ref(schema_type_symbol(untag(database.name)))]
    );
  }


  Instr* database_init_code {
    state_vars = database.state_vars;
    nested_dbs = database.nested_dbs;

    vars_pre_init_code = rand_sort([init_memb_var(v, :blank) : v, unused_var <- state_vars]);
    let context = database, var_subst_map = [], cls_var_subst_map = [], typed_vars = [], surrogates = []:
      vars_init_code = join(({
          eval_code, surr, unused_var = opt_eval_code(d.init_value, [], blank_var_gen);
          return (eval_code | set_var(v, surr));
        } : v, d <- isort(state_vars)
      ));
    ;
    nested_dbs_init_code = rand_sort([init_db(v, (), t) : v, t <- nested_dbs]);

    value_stores_init_code = (init_value_store(vs) : vs <- rand_sort(value_stores));
    tables_init_code = rand_sort([init_table(v, t) : v, t <- tables_info]);

    return vars_pre_init_code & vars_init_code & nested_dbs_init_code & value_stores_init_code & tables_init_code;
  }


  Instr* database_cleanup_code {
    state_vars = database.state_vars;
    nested_dbs = database.nested_dbs;

    vars_cleanup_code = rand_sort([reset_memb_var(v, :blank) : v, unused_var <- state_vars]);
    nested_dbs_cleanup_code = rand_sort([cleanup_db(v, t) : v, t <- nested_dbs]);

    value_stores_cleanup_code = (cleanup_value_store(vs) : vs <- rand_sort(value_stores));
    tables_cleanup_code = rand_sort([cleanup_table(v, t) : v, t <- tables_info]);

    return vars_cleanup_code & nested_dbs_cleanup_code & value_stores_cleanup_code & tables_cleanup_code;
  }


  Instr* database_copy_state_code {
    count = nat(|database.state_vars| + |database.rel_vars| + |database.nested_dbs|);
    return (ret_val(empty_rel)) if count == 0;

    res_var, var_gen = next_obj_var(blank_var_gen);
    keys_var, var_gen = next_vector_var(var_gen, count);
    values_var, var_gen = next_vector_var(var_gen, count);

    state_vars = rand_sort(keys(database.state_vars));
    rel_vars = rand_sort(keys(database.rel_vars));
    nested_dbs = rand_sort(keys(database.nested_dbs));
    all_vars = state_vars & rel_vars & nested_dbs;

    tables = tables_info;

    let context = database:
      code = join(({
        set_label_instr = set_var(evar(keys_var.id, i), object(bare_id(v)));
        value_var = evar(values_var.id, i);
        set_value_instr = match (v)
          memb_var()      = set_var(value_var, v),
          rel_var()       = copy_table(
                              value_var,
                              nonempty_map(select_by_key(tables_info, physical_tables(database.name, v))),
                              rel_var_cols_perm(database.name, v)
                            ),
          nested_db_var() = copy_state(value_var, v, var_type(v));
        return (set_label_instr, set_value_instr);
      } : v @ i <- all_vars));
    ;

    return (code | mk_map(res_var, keys_var, values_var, count), ret_val(res_var));
  }


  Instr* database_set_state_code {
    rel_auto_id = database.name;

    state_var = fn_arg(0);
    tmp_var = lvar(0);
    log_args_vars = (lvar(nat(i+1)) : i < 3);
    surr_vars = (ivar32(i) : i < 3);
    set_it_var = set_it_var(0);
    bin_rel_it_var = bin_rel_it_var(0);
    tern_rel_it_var = tern_rel_it_var(0);

    code = ();

    // Member variables
    for v <- isort(keys(database.state_vars)):
      instr = do_if(
        has_field(state_var, object(bare_id(v))),
        ( // lookup_field(v, state_var, object(bare_id(v)))  ## REFERENCE COUNTING BUG HERE...
          lookup_field(tmp_var, state_var, object(bare_id(v))),
          set_var(v, tmp_var)
        )
      );
      code = (code | instr);
    ;

    // Mutable relation variables
    ord_rel_vars = sort_by_key(keys(database.rel_vars), arity(rel_auto_id, $));
    for v <- ord_rel_vars:
      arity = arity(rel_auto_id, v);
      cols_perm = rel_var_cols_perm(rel_auto_id, v);
      phys_args_vars = (log_args_vars(i) : i <- cols_perm);

      // Physical tables the logical relation is partitioned into
      phys_tables = physical_tables(rel_auto_id, v);

      if |phys_tables| == 1:
        table_var = only(phys_tables);
        stores_ids = (table_col_store_id(rel_auto_id, table_var, i) : i < arity);
        core_loop_code = (
          (immediate_insert_or_add_ref(surr_vars(i), stores_ids(i), phys_args_vars(i)) : i < arity) |
          immediate_insert_tuple(table_var, nonempty(take(surr_vars, arity)))
        );

      else
        // Choosing the dispatch arguments
        signatures = [table_signatures(rel_auto_id, tv) : tv <- phys_tables];
        disp_args_idxs = optimal_dispatch_args(signatures, typedefs);
        disp_args = (log_args_vars(i) : i <- disp_args_idxs);

        cases = [{
          stores_ids = (table_col_store_id(rel_auto_id, tv, i) : i < arity);
          case_code = (
            (immediate_insert_or_add_ref(surr_vars(i), stores_ids(i), phys_args_vars(i)) : i < arity) |
            immediate_insert_tuple(tv, nonempty(take(surr_vars, arity)))
          );
          types = table_signatures(rel_auto_id, tv);
          disp_types = [(ts(i) : i <- disp_args_idxs) : ts <- types];
          return (disp_types, case_code);
        } : tv <- phys_tables];

        block_body = polymorphic_dynamic_dispatch_code(disp_args, cases);
        core_loop_code = (execute_block(block_body));
      ;

      loop_body = (break_if(is_out_of_range(bin_rel_it_var))) &
                  (get_curr_arg(log_args_vars(i), bin_rel_it_var, i) : i < arity) &
                  core_loop_code & (move_forward(bin_rel_it_var));

      instr = do_if(
        has_field(state_var, object(bare_id(v))),
        ( lookup_field(tmp_var, state_var, object(bare_id(v))),
          get_iter(bin_rel_it_var, tmp_var, (nothing, nothing)),
          repeat(loop_body)
        )
      );

      code = (code | instr);
    ;

    // Nested automata
    nested_dbs = rand_sort(keys(database.nested_dbs));
    for db <- nested_dbs:
      db_type = database.nested_dbs[db];
      code = (code |
        lookup_field(tmp_var, state_var, object(bare_id(db))),
        set_state(db, tmp_var, db_type)
      );
    ;

    return code;
  }


  Instr* database_full_check_code {
    rel_auto_id = database.name;

    phys_keys_by_table = group_by(physical_foreign_keys, $.origin);
    tables = sort_by_key(keys(phys_keys_by_table), arity(rel_auto_id, logical_rel_var($)));

    code = ();

    for tv <- tables:
      arity = arity(rel_auto_id, logical_rel_var(tv));

      surr_vars = (ivar32(i) : i < 3);
      it_var = table_it_var(0, arity);
      foreign_keys = lookup(phys_keys_by_table, tv, []);

      if foreign_keys != []:
        conds = ({
          args = match (fk.type)
            unary_unary       = (just(surr_vars(0))),
            unary_binary(i?)  = (maybe(surr_vars(0), j == i) : j < 2),
            unary_ternary(i?) = (maybe(surr_vars(0), j == i) : j < 3),
            binary_unary(i?)  = (just(surr_vars(i))),
            ternary_unary(i?) = (just(surr_vars(i))),
            binary_ternary    = (just(surr_vars(0)), just(surr_vars(0)), nothing);
            ternary_binary    = (just(surr_vars(0)), just(surr_vars(0)));
          return table_contains(fk.target, args);
        } : fk <- isort(foreign_keys));

        cond = and_then((neg(c) : c <- conds));

        surr_vars_used = isort(union([
          match (fk.type)
            unary_unary       = [0],
            unary_binary(i?)  = [0],
            unary_ternary(i?) = [0],
            binary_unary(i?)  = [i],
            ternary_unary(i?) = [i],
            binary_ternary    = [0, 1],
            ternary_binary    = [0, 1]
          : fk <- foreign_keys
        ]));

        surrs_reads = (set_ivar32(surr_vars(i), get_tuple_field(it_var, i)) : i <- surr_vars_used);

        code = (code |
          get_table_iter(it_var, tv, (nothing)),
          repeat((
            break_if(is_out_of_range(it_var))
          ) & surrs_reads & (
            do_if(cond, ret_val(false)),
            move_forward(it_var)
          ))
        );
      ;
    ;

    return (code | ret_val(true));
  }

  // public boolean fullCheck() {
  //   int i0, i1;

  //   UnaryTable.Iter uit = operator_B.getIter();
  //   while (!uit.done()) {
  //     i0 = uit.get();
  //     if (!code_B1.contains1(i0) || !name_B.contains1(i0))
  //       return false;
  //   }

  //   BinaryTable.Iter bit = code_B0.getIter();
  //   while (!bit.done()) {
  //     i0 = bit.get1();
  //     if (!operator_B.contains(i0))
  //       return false;
  //   }

  //   return true;
  // }


  ## COMPILATION OF METHODS FOR BOTH TYPES OF AUTOMATA IS ALMOST IDENTICAL, CAN WE SHARE SOME CODE HERE?
  [MethodId -> CompiledMethod] compiled_database_methods([Method] methods) {
    return merge([compiled(ms) : unused_var, ms <- group_by(methods, ($.name, |$.args|))]);


    [MethodId -> CompiledMethod] compiled([+Method] methods) {
      a_method = an_elem(methods);
      name = a_method.name;
      arity = |a_method.args|;
      return [method_id(name, arity) -> compiled(a_method)] if |methods| == 1;

      sorted_methods = rand_sort(methods);
      comp_methods = merge(([method_id(m.name, arity, i) -> compiled(m)] : m @ i <- sorted_methods));

      disp_method = compiled_method(
        (nothing : i < arity),
        (ne_union_superset([left(m.args(i)) : m <- methods]) : i < arity),
        [(memb_test_preds_map[t] : t, unused_var <- m.args) : m <- methods],
        ne_union_superset([m.ret_type : m <- methods]),
        method_dispatch_code(database.name, sorted_methods)
      );

      return comp_methods & [method_id(name, arity) -> disp_method];
    }


    CompiledMethod compiled(Method method) =
      compiled_method(
        (v : unused_var, v <- method.args),
        (t : t, unused_var <- method.args),
        [(memb_test_preds_map[t] : t, unused_var <- method.args)],
        method.ret_type,
        method_body(method)
      );


    Instr* method_body(Method method) {
      env = [if a != nothing then value(a) else fn_arg(i) -> t : t, a @ i <~ method.args];
      let context = database, var_subst_map = [], cls_var_subst_map = [], typed_vars = [], surrogates = []:
        eval_code, surr, unused_var = opt_eval_code(method.expr, env, blank_var_gen);
      ;
      return (eval_code | ret_val(surr));
    }
  }


  [UpdateId -> CompiledUpdate] compiled_database_updates([Update] updates) {
    return merge([compiled_updates(ms) : unused_var, ms <- group_by(updates, ($.name, |$.args|))]);


    [UpdateId -> CompiledUpdate] compiled_updates([+Update] updates) {
      an_update = an_elem(updates);
      name = an_update.name;
      arity = |an_update.args|;
      return [update_id(name, arity) -> compiled_update(an_update)] if |updates| == 1;
      sorted_updates = rand_sort(updates);
      comp_updates = merge(([update_id(m.name, arity, i) -> compiled_update(m)] : m @ i <- sorted_updates));
      dispatcher_code = update_dispatch_code(database.name, sorted_updates);
      dispatcher = compiled_update((fn_arg(i) : i < arity), dispatcher_code);
      return comp_updates & [update_id(name, arity) -> dispatcher];
    }


    CompiledUpdate compiled_update(Update update) =
      compiled_update((v : unused_var, v <- update.args), update_body(update));


    Instr* update_body(Update update) {
      // Compiling the body of the update
      return () if update.body == ();

      code, unused_var_1, unused_var_2, unused_var_3 = stmts_opt_code(
        update.body,
        [v -> t : t, v <~ update.args],
        blank_var_gen,
        context           = database,
        var_subst_map     = [],
        cls_var_subst_map = [],
        typed_vars        = [],
        surrogates        = [],
        exp_break_surrs   = nothing,
        result_var        = :invalid_var
      );

      return (execute_block(nonempty(code)));
    }
  }


  [UpdateId -> CompiledUpdate] compiled_database_handlers([Handler] handlers) {
    return [] if handlers == [];
    return [main_handler -> compiled_handler(only(handlers))] if |handlers| == 1;
    sorted_handlers = rand_sort(handlers);
    comp_handlers = merge(([handler_id(i) -> compiled_handler(h)] : h @ i <- sorted_handlers));
    dispatcher_body = update_dispatch_code(database.name, sorted_handlers);
    dispatcher = compiled_update((fn_arg(0)), dispatcher_body);
    return comp_handlers & [main_handler -> dispatcher];


    CompiledUpdate compiled_handler(Handler handler) = compiled_update((var(:self)), handler_body(handler));

    Instr* handler_body(Handler handler) {
      // Compiling the body of the handler
      return () if handler.body == ();

      code, unused_var_1, unused_var_2, unused_var_3 = stmts_opt_code(
        handler.body,
        [var(:self) -> handler.type],
        blank_var_gen,
        context           = database,
        var_subst_map     = [],
        cls_var_subst_map = [],
        typed_vars        = [],
        surrogates        = [],
        exp_break_surrs   = nothing,
        result_var        = :invalid_var
      );

      return (execute_block(nonempty(code)));
    }
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit program : Program, typedefs : [TypeName -> NeType], database : Database {
  [BlockForeignKey] physical_foreign_keys {
    tables : [+TableVar];
    table_vars_by_rel_var : [RelVar -> [+TableVar]];

    rel_auto_id = database.name;

    tables = physical_tables(rel_auto_id);
    table_vars_by_rel_var = group_by(tables, logical_rel_var);

    physical_keys = ();

    // Iterating through all foreign keys
    for k <- isort(database.foreign_keys):
      src_args = right(k.source);

      // Source signature of the foreign key
      src_key_sgn = (t : unused_var, t <- src_args);

      // Iterating through the tables the source relation is partitioned in
      for sv <- isort(table_vars_by_rel_var(left(k.source))):

        // Signatures of the current source table
        src_table_sgns = table_signatures(rel_auto_id, sv);

        // Checking to see if the current table signature overlaps
        // with the source signature of the foreign key
        if (s <- src_table_sgns : signature_overlaps_partial_signature(s, src_key_sgn)):

          // Making sure that source signature of the foreing key applies to the entire table
          ## THIS HAS TO BE CHECKED DURING WELL-FORMEDNESS CHECKING, EITHER IN STAGE ONE OR TWO
          assert not (s <- src_table_sgns : not is_subset_signature(s, src_key_sgn));

          // Target tables that are reached by the instantiation of the foreign
          // key on this particular physical table
          target_tables = [];

          // Iterating through the polymorphic signatures of the source table
          for src_sgn <- isort(src_table_sgns):

            // Target signature for this particular source table signature
            args_types = [v -> src_sgn(i) : v, unused_var @ i <~ src_args, v != nothing];
            target_key_sgn = (if v != nothing then just(args_types(v)) else nothing : v <- right(k.target));

            // Iterating through the tables the target relation is partitioned in
            for tv <- isort(table_vars_by_rel_var(left(k.target))):

              // Polymorphic signatures of the target table
              target_table_signatures = table_signatures(rel_auto_id, tv);

              // If any of the polymorphic signatures of the target table overlaps with the
              // current target signature, then the table is a target of the physical key
              if (s <- target_table_signatures : overlap(s, target_key_sgn)):
                target_tables = target_tables & [tv];
              ;
            ;
          ;

          // Just double checking that there's only one possible target table for this key
          assert |target_tables| == 1;

          // Creating the physical foreign key
          key_type = foreign_key_type(k);
          physical_key = foreign_key(sv, only(target_tables), key_type);
          physical_keys = (physical_keys | physical_key);
        ;
      ;
    ;

    return set(physical_keys);


    Bool overlap(NeType+ signature, Maybe[NeType]+ partial_signature) {
      assert |signature| == |partial_signature|;
      for t2 @ i <- partial_signature:
        return false if t2 != nothing and are_ptype_disjoint(signature(i), value(t2));
      ;
      return true;
    }

    Bool is_subset_signature(NeType+ signature, Maybe[NeType]+ partial_signature) {
      assert |signature| == |partial_signature|;
      for mst @ i <- partial_signature:
        return false if mst != nothing and not is_subset(signature(i), value(mst));
      ;
      return true;
    }
  }


  ForeignKeyType foreign_key_type(ForeignKey key) {
    origin_relvar, typed_origin_args = key.source;
    origin_args = (a : a, t <- typed_origin_args);
    target_relvar, target_args = key.target;

    assert match (origin_args, target_args)
      (just(s1?)),              (just(s2?))                     = s1 == s2,
      (just(s1?)),              (just(s2?), nothing)            = s1 == s2,
      (just(s1?)),              (nothing, just(s2?))            = s1 == s2,
      (just(s1?)),              (just(s2?), nothing, nothing)   = s1 == s2,
      (just(s1?)),              (nothing, just(s2?), nothing)   = s1 == s2,
      (just(s1?)),              (nothing, nothing, just(s2?))   = s1 == s2,

      (just(s1?), nothing),     (just(s2?))                     = s1 == s2,
      (nothing, just(s1?)),     (just(s2?))                     = s1 == s2,
      (just(s1?), just(s2?)),   (just(s3?), just(s4?), nothing) = s1 != s2 and s1 == s3 and s2 == s4,

      (_, _, _) as?,            (t?)                            = { las = (a : a <- as, a != nothing);
                                                                    return |las| == 1 and only(las) == t;
                                                                  },
      (just(s1?), just(s2?), nothing),  (just(s3?), just(s4?))  = s1 != s2 and s1 == s3 and s2 == s4,

      _,                        _                               = false;

    rev_origin_perm = reverse_perm(rel_var_cols_perm(database.name, origin_relvar));
    rev_target_perm = reverse_perm(rel_var_cols_perm(database.name, target_relvar));

    key_type = match (origin_args, target_args)
      (just()),           (just())          = :unary_unary,
      (just()),           (just(), nothing) = unary_binary(rev_target_perm(0)),
      (just()),           (nothing, just()) = unary_binary(rev_target_perm(1)),
      (just()),           (_, _, _) as?     = { idx = only_unsafe((i : a @ i <- as, a != nothing));
                                                return unary_ternary(rev_target_perm(idx));
                                              },

      (just(),  nothing), (just())          = binary_unary(rev_origin_perm(0)),
      (nothing, just()),  (just())          = binary_unary(rev_origin_perm(1)),
      (just(),  just()),  (_, _, _)         = :binary_ternary,

      (_, _, _) oas?,     (just() ta?)      = { idx = only_unsafe((i : a @ i <- oas, a == ta));
                                                return ternary_unary(rev_origin_perm(idx));
                                              },
      (_, _, _),          (_, _)            = :ternary_binary;

    return key_type;
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

[[+(TableVar, Nat)]] column_clusters([TableVar, NzNat] relvars, [BlockForeignKey] foreign_keys) {
  init_clusters = union([associations(k) : k <- foreign_keys]);
  non_trivial_clusters = merge_intersecting(init_clusters);
  all_cols = [(r, i) : r, a <- relvars, i <~ (i : i < a)];
  rem_cols = all_cols - union(non_trivial_clusters);
  final_clusters = non_trivial_clusters & [[c] : c <- rem_cols];
  assert union(final_clusters) == all_cols;
  return final_clusters;


  [[+(TableVar, Nat)]] associations(BlockForeignKey foreign_key) {
    idx_assocs = match (foreign_key.type)
      unary_unary         = [0 -> 0],
      unary_binary(i?)    |
      unary_ternary(i?)   = [0 -> i],
      binary_unary(i?)    |
      ternary_unary(i?)   = [i -> 0],
      binary_ternary      |
      ternary_binary      = [0 -> 0, 1 -> 1];

    return [[(foreign_key.origin, i), (foreign_key.target, j)] : i, j <- idx_assocs];
  }
}


implicit program : Program, typedefs : [TypeName -> NeType], database : Database { ## program IS NOT REALLY NEEDED, BUT THE TYPECHECKER...
  [ValueStoreId] value_stores = values(table_col_value_store_map);


  [(TableVar, Nat) -> ValueStoreId] table_col_value_store_map {
    auto_id = database.name;
    table_vars_arity = [r, arity(auto_id, logical_rel_var(r)) : r <- physical_tables(auto_id)];
    clusters = column_clusters(table_vars_arity, physical_foreign_keys);
    ord_clusters = isort(clusters);
    return [c -> value_store_id(i) : cs @ i <~ ord_clusters, c <- cs];
  }


  [TableVar -> Table] tables_info {
    auto_id = database.name;
    col_to_vs_map = table_col_value_store_map;

    return [
      tv -> {
        arity = arity(database.name, rv);

        value_stores = (col_to_vs_map((tv, i)) : i < arity);
        keys = table_var_keys(auto_id, tv);
        applied_perm = rel_var_cols_perm(auto_id, rv);

        log_cols_types = (ne_union_superset([ts(i) : ts <- table_signatures(auto_id, tv)]) : i < arity);
        phys_cols_types = (log_cols_types(i) : i <- applied_perm);

        return table(value_stores, keys, phys_cols_types, applied_perm);
      } : rv, unused_var <- database.rel_vars, tv <- physical_tables(auto_id, rv)
    ];
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit typedefs : [TypeName -> NeType] {
  ## THIS IS AN ALMOST PERFECT DUPLICATE OF gen_dispatch_auto_method_body().
  Instr* method_dispatch_code(DBSymbol db_type, Method+ methods) {
    assert |methods| > 1;
    assert |[m.name : m <~ methods]| == 1;    // All methods must have the same name
    assert |[|m.args| : m <~ methods]| == 1;  // All methods have the same arity
    assert |head(methods).args| > 0;          // No-argument methods cannot be polymorphic


    l0 = lvar(0);
    count = |methods|;

    a_method  = head(methods);
    name      = a_method.name;
    arity     = |a_method.args|;

    disp_args = choose_disp_args(((t : t, unused_var <- nonempty(m.args)) : m <- methods), typedefs);

    cases = [{
      types = (left(m.args(j)) : j <- disp_args);
      invoke_method_instr = invoke_db_method(l0, db_type, method_id(name, arity, i), (fn_arg(j) : j < arity));
      code = (invoke_method_instr, ret_val(l0));
      return (types, code);
    } : m @ i <~ methods];

    code = dynamic_dispatch_code((fn_arg(i) : i <- disp_args), cases);
    return (code | ret_val(l0));
  }


  Instr* update_dispatch_code(DBSymbol db_type, <Update+, Handler+> updates) {
    assert |updates| > 1;
    assert |[name(u) : u <~ updates]| == 1;   // All updates must have the same name
    assert |[arity(u) : u <~ updates]| == 1;  // All updates have the same arity
    assert arity(head(updates)) > 0;          // No-argument updates cannot be polymorphic

    l0 = lvar(0);
    count = |updates|;

    an_update = head(updates);
    name      = name(an_update);
    arity     = arity(an_update);

    args_types = (
      match (u)
        update()  = (t : t, unused_var <- nonempty(u.args)),
        handler() = (u.type)
      : u <- updates
    );

    disp_args = choose_disp_args(args_types, typedefs);

    cases = [{
      types = (args_types(i)(j) : j <- disp_args);
      try_update_instr = try_update(db_type, polymorphic_update_id(u, i), (fn_arg(j) : j < arity));
      code = (try_update_instr, exit_block);
      return (types, code);
    } : u @ i <~ updates];

    code = dynamic_dispatch_code((fn_arg(i) : i <- disp_args), cases);
    return (execute_block(code));
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

Maybe[UpdateSymbol] name(<Update, Handler>) =
  update() u?   = just(u.name),
  handler()     = nothing;

NeType* arg_types(<Update, Handler>) =
  update() u?   = (t : t, unused_var <- u.args),
  handler() h?  = (h.type);

UpdateId polymorphic_update_id(<Update, Handler>, Nat idx) =
  update() u?   = update_id(u.name, arity(u), idx),
  handler() h?  = handler_id(idx);

implicit program : Program, typedefs : [TypeName -> NeType] {
  (Program, [DBSymbol -> [RelVar -> [+Nat -> Nat]]]) post_rel_vars_perm_program {
    new_prg = program(
      typedefs:       program.typedefs,
      fndefs:         program.fndefs,
      proc_defs:      [permutate_rel_vars(p) : p <- program.proc_defs],
      aggr_fn_defs:   program.aggr_fn_defs,
      auto_defs:      [permutate_rel_vars(a) : a <- program.auto_defs],
      data_bases:     [permutate_rel_vars(a) : a <- program.data_bases]
    );
    applied_perms = rel_vars_perm_map;
    return (new_prg, applied_perms);
  }

  //////////////////////////////////////////////////////////////////////////////
  //////////////////////////////////////////////////////////////////////////////

  Database permutate_rel_vars(Database rel_auto) {
    let context = rel_auto {
      perms = rel_vars_perm_map(rel_auto.name, !);

      rel_vars = [v -> {
        perm = perms(logical_rel_var(v));
        return (
          signature:  permutate(info.signature, perm),
          keys:       [[perm(i) : i <- k] : k <- info.keys],
          symmetric:  info.symmetric
        );
      } : v info <- rel_auto.rel_vars];

      foreign_keys = [rewrite(k, perms) : k <- rel_auto.foreign_keys];

      methods = [method(m.id, m.args, m.ret_type, perm_expr_rel_vars(m.expr)) : m <- rel_auto.methods];
      updates  = [update(u.name, u.args, (perm_stmt_rel_vars(s) : s <- u.body)) : u <- rel_auto.updates];
      handlers = [{
        body = (perm_stmt_rel_vars(s) : s <- h.body);
        return handler(type: h.type, body: body ,fields_order: h.fields_order if h.fields_order?);
      } : h <- rel_auto.handlers];
    }

    return data_base(
      name:         rel_auto.name,
      links:        rel_auto.links,
      state_vars:   rel_auto.state_vars,
      rel_vars:     rel_vars,
      attr_map:     rel_auto.attr_map,
      foreign_keys: foreign_keys,
      methods:      methods,
      updates:      updates,
      handlers:     handlers
    );


    ForeignKey rewrite(ForeignKey key, [RelVar -> [+Nat -> Nat]] perms) {
      src_perm = perms(logical_rel_var(key.source_rel_var));
      tgt_perm = perms(only([logical_rel_var(v) : v <- key.target_rel_vars]));
      return foreign_key(
        key.source_rel_var,
        permutate(key.source_args, src_perm),
        key.target_rel_vars,
        permutate(key.target_args, tgt_perm)
      );
    }
  }

  //////////////////////////////////////////////////////////////////////////////

  Automaton permutate_rel_vars(Automaton react_auto) {
    ## IMPLEMENT IMPLEMENT IMPLEMENT
    return react_auto;
  }

  //////////////////////////////////////////////////////////////////////////////

  Procedure permutate_rel_vars(Procedure proc) {
    let context = proc
      body = (perm_stmt_rel_vars(s) : s <- proc.body);
    return procedure(
      id:                   proc.id,
      args:                 proc.args,
      rel_auto_args:        proc.rel_auto_args,
      react_auto_args:      proc.react_auto_args,
      res_type:             proc.res_type,
      loc_rel_auto_vars:    proc.loc_rel_auto_vars,
      loc_react_auto_vars:  proc.loc_react_auto_vars,
      body:                 body
    );
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit program : Program, typedefs : [TypeName -> NeType], context : Context {
  Expr perm_expr_rel_vars(Expr expr) =
    rel_var_size_expr()   = rel_var_size_expr(expr.rel_var, rewrite_maybe_args(expr.args, expr.rel_var)),
    relvar_memb_test()    = relvar_memb_test(expr.rel_var, rewrite_maybe_args(expr.args, expr.rel_var)),
    relvar_lookup()       = relvar_lookup(expr.rel_var, rewrite_maybe_args(expr.args, expr.rel_var)),
    _                     = rewrite_expr(expr, perm_expr_rel_vars, perm_cls_rel_vars, perm_stmt_rel_vars);


  Clause perm_cls_rel_vars(Clause cls) {
    return match (cls)
      relvar_clause() = rewrite(cls),
      _               = rewrite_clause(cls, perm_expr_rel_vars, perm_cls_rel_vars);


    // type RelVarClause = relvar_clause(vars: StdVar+, rel_var: AnyRelVar, args: Maybe[Expr]+);
    RelVarClause rewrite(RelVarClause cls) {
      perm = rel_var_args_perm(cls.rel_var);
      args = permutate(cls.args, perm);
      args = (apply(a, perm_expr_rel_vars) : a <- args);
      var_arg_idxs = (i : a @ i <- cls.args, a == nothing);
      vars = values_ord_by_idx([perm(var_arg_idxs(i)) -> v : v @ i <- cls.vars]);
      return relvar_clause(vars, cls.rel_var, args);
    }
  }


  Statement perm_stmt_rel_vars(Statement stmt) {
    return match (stmt)
      delete_stmt()   = delete_stmt(stmt.var, rewrite_maybe_args(stmt.args, stmt.var)),
      insert_stmt()   = insert_stmt(stmt.var, rewrite_args(stmt.args, stmt.var)),
      update_stmt()   = update_stmt(stmt.var, rewrite_args(stmt.args, stmt.var)),
      foreach_stmt()  = rewrite(stmt),
      _               = rewrite_stmt(stmt, perm_expr_rel_vars, perm_stmt_rel_vars);


    // type ForeachStmt = foreach_stmt(iter: Iter, body: Statement+);
    ForeachStmt rewrite(ForeachStmt stmt) {
      iter = match (stmt.iter)
        coll_value_iter() |
        rel_subset_iter() = stmt.iter,
        rel_var_iter()    = rewrite(stmt.iter);
      return foreach_stmt(iter, (perm_stmt_rel_vars(s) : s <- stmt.body));
    }


    // type RelVarIter = rel_var_iter(vars: Maybe[StdVar]+, rel_var: AnyRelVar, args: Maybe[Expr]+);
    RelVarIter rewrite(RelVarIter iter) {
      perm = rel_var_args_perm(iter.rel_var);
      args = permutate(iter.args, perm);
      args = (apply(a, perm_expr_rel_vars) : a <- args);
      var_arg_idxs = (i : a @ i <- iter.args, a == nothing);
      vars = values_ord_by_idx([perm(i) -> v : v @ i <- iter.vars]);
      return rel_var_iter(vars, iter.rel_var, args);
    }
  }

  //////////////////////////////////////////////////////////////////////////////

  Expr+ rewrite_args(Expr+ args, AnyRelVar rel_var) {
    perm = rel_var_args_perm(rel_var);
    return (perm_expr_rel_vars(e) : e <- permutate(args, perm));
  }


  Maybe[Expr]+ rewrite_maybe_args(Maybe[Expr]+ args, AnyRelVar rel_var) {
    perm = rel_var_args_perm(rel_var);
    return (apply(a, perm_expr_rel_vars) : a <- permutate(args, perm));
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit program : Program, typedefs : [TypeName -> NeType], context : Context {
  [+Nat -> Nat] rel_var_args_perm(AnyRelVar rel_var) {
    rel_auto = rel_auto_type(rel_var);
    perms_map = rel_vars_perm_map(rel_auto, !);
    return perms_map(unqualified(rel_var));
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit program : Program {
  [DBSymbol -> [RelVar -> [+Nat -> Nat]]] rel_vars_perm_map {
    return [
      a.name -> [
        logical_rel_var(r) -> rel_var_args_perm(|info.signature|, info.keys) : r info <- a.rel_vars
      ] : a <- program.data_bases
    ];


    [+Nat -> Nat] rel_var_args_perm(NzNat arity, [[+Nat]] keys) {
      if arity == 1 {
        assert keys == [];
        perm = [0 -> 0];
      }
      else if arity == 2 {
        perm = if keys == [[1]] then [0 -> 1, 1 -> 0] else [0 -> 0, 1 -> 1];
      }
      else {
        assert arity == 3;
        perm = if tern_rel_perm_by_key(keys, _)
          then tern_rel_perm_by_key(keys, !)
          else [0 -> 0, 1 -> 1, 2 -> 2];
      }
      perm_keys = [[perm(i) : i <- k] : k <- keys];
      assert keys == [] or valid_keys_by_arity(arity, !)(perm_keys);
      return perm;
    }


    [[[+Nat]] -> [+Nat -> Nat]] tern_rel_perm_by_key = [
      [[0, 2]]                  -> [0 -> 0, 1 -> 2, 2 -> 1],
      [[1, 2]]                  -> [0 -> 2, 1 -> 0, 2 -> 1],
      [[0, 1], [0, 2]]          -> [0 -> 1, 1 -> 0, 2 -> 2],
      [[0, 2], [1, 2]]          -> [0 -> 0, 1 -> 2, 2 -> 1],
      [[0, 2], [1]]             -> [0 -> 0, 1 -> 2, 2 -> 1],
      [[1, 2], [0]]             -> [0 -> 2, 1 -> 0, 2 -> 1]
    ];


    [NzNat -> [+[[+Nat]]]] valid_keys_by_arity = [
      2 -> [[[0]], [[0], [1]]],
      3 -> [
        [[0, 1]],
        [[0, 1], [2]],
        [[0, 1], [1, 2]],
        [[0, 1], [1, 2], [0, 2]]
      ]
    ];
  }
}
